#!/usr/bin/env perl
# Woger - make a release
my $version_banner = <<END;
Woger welease 23
Copyright (c) Reuben Thomas 2007-2012
Released under the GPL version 3, or (at your option) any later version.
END

# FIXME: Add methods rsync (e.g. for web sites), log (to announce on my log)

# Dependencies:
#   For email: mail that supports -a for attachment (e.g. heirloom-mailx)
#   For SourceForge: copher: http://copher.sourceforge.net/
#   For Freecode: freecode-submit: http://www.catb.org/~esr/freecode-submit/
#   For github: github-upload: https://github.com/wereHamster/ghup

use 5.10.0;

use warnings;
use strict;

use Perl6::Slurp;
use Text::Wrap;
use Set::Scalar;

use File::Basename;
use Getopt::Long;

my $prog = basename($0);

my %var_help = (
  package => "package base name",
  package_name => "package text name",
  version => "version number",
  description => "short project description",
  dist_type => "file extension for distribution archive",
  email => "release email",
  notes => "release notes file",
  home => "home page URL",
  upload => "upload URL",
  github_user => "github user",
);


# Release methods

my %releaser = ();
my %vars;

%{$releaser{null}} = (
  vars => Set::Scalar->new,
  release => sub {
  }
 );

%{$releaser{freecode}} = (
  vars => Set::Scalar->new("package", "notes", "version"),
  release => sub {
    my $self = shift;
    open(FILE, "|-", "freecode-submit");
    print FILE "Project: $vars{package}\nVersion: $vars{version}\n\n" . slurp($vars{notes});
    close FILE or die "freecode-submit failed\n";
  }
 );

%{$releaser{"github"}} = (
  vars => Set::Scalar->new("dist_type", "package", "version"),
  release => sub {
    system("github-upload", "$vars{package}-$vars{version}.$vars{dist_type}") == 0 or die "upload failed\n";
  }
 );

%{$releaser{gnu}} = (
  vars => Set::Scalar->new("dist_type", "email", "package", "package_name", "upload", "version"),
  release => sub {
    # FIXME: Get the command in the next line from the output of make {alpha,beta,stable}
    system("./build-aux/gnupload", "--to", "$vars{upload}:$vars{package}",
           "$vars{package}-$vars{version}.$vars{dist_type}") == 0
             or die "gnupload failed\n";

    my $url = "$vars{upload}/gnu/$vars{package}/$vars{package}-$vars{version}.tar";

    # Wait for URL to become available:
    for (my $i = 0; $i < 100; $i++) {
      system "wget", "--quiet", "-O", "-", "$url.gz.sig" and last;
      sleep 10;
    }

    # Download and verify xz and gz signatures
    # FIXME: Get correct suffixes (e.g. xz xz.sig bz2 bz2.sig) automatically
    for my $i ("gz", "gz.sig") {
      system "wget", "--quiet", "$url.$i" or last;
      system "gpg", "--verify", basename($url) . ".$i" if $i =~ /\.sig$/;
    }
    my $message = slurp($vars{email});
    $message =~ s/^\n//s;
    mail($message); # FIXME: Cope with Mail-Followup-To header
  }
 );

%{$releaser{"lua"}} = (
  vars => Set::Scalar->new("description", "dist_type", "github_user", "notes", "package", "package_name", "version"),
  release => sub {
    my $release_notes = slurp($vars{notes});
    $vars{home} ||= "https://github.com/$vars{github_user}/$vars{package}";
    $vars{archive} ||= "$vars{package}-$vars{version}.$vars{dist_type}";
    my $url = "https://github.com/downloads/$vars{github_user}/$vars{package}/$vars{archive}";
    my $message = <<END;
I am happy to announce the release of $vars{package_name} $vars{version},
$vars{description}.

$release_notes

Download it from $url
$vars{package_name}'s home page is at $vars{home}
END
    mail($message, "lua-l\@lists.lua.org", "[ANN] $vars{package_name} $vars{version} released");
    mail("Rockspec for $vars{package} version $vars{version} attached.",
         "luarocks-developers\@lists.sourceforge.net",
         "[ANN] $vars{package_name} $vars{version} released: rockspec attached",
         "$vars{package}-$vars{version}-1.rockspec");
  }
 );

%{$releaser{sourceforge}} = (
  vars => Set::Scalar->new("dist_type", "notes", "package", "version"),
  release => sub {
    my $self = shift;
    system("copher", "-p", $vars{package}, "-k", $vars{package}, "-r", $vars{version},
           "-N", $vars{notes}, "$vars{package}-$vars{version}.$vars{dist_type}") == 0
             or die "copher failed\n";
    unlink "out.html";
  }
 );


sub mail {
  my ($body, $address, $subject, @attachments) = @_;
  my @args;
  push @args, "-a", $_ foreach @attachments;
  push @args, "-s", $subject if $subject;
  if ($address) {
    push @args, $address;
  } else {
    push @args, "-t";
  }
  open(FILE, "|-", "mail", @args);
  print FILE wrap('', '', ($body));
  close FILE or die "mail to $address failed\n";
}

my $methods = join ", ", keys %releaser;

sub usage {
  my ($exit_code) = @_;
  print STDERR <<END;
$prog [OPTION...] METHODS [VARIABLE=value...]
Release a program.

  METHODS is a comma-separated list of methods, from:
    $methods

 Options:
  --vars                     display the variables needed by METHODS and exit
  --help                     display this help and exit
  --version                  display version information and exit
END
  exit $exit_code;
}

# Get arguments
my ($help_flag, $version_flag, $vars_flag);
my $opts = GetOptions(
  "help" => \$help_flag,
  "version" => \$version_flag,
  "vars" => \$vars_flag,
) or usage(1);
if ($version_flag) {
  print STDERR $version_banner;
  exit 0;
}
usage(0) if $help_flag or $#ARGV == -1;
my @methods = split /,/, shift;
foreach my $arg (@ARGV) {
  $arg =~ s/([A-Za-z0-9_]+)\s*=\s*(.*)/$vars{$1} = $2/e;
}
my $needed = Set::Scalar->new();
$needed = $needed->union($releaser{$_}{vars}) foreach @methods;
my $missing;
foreach my $e ($needed->elements) {
  $missing = 1 unless $vars{$e};
}
if ($vars_flag || $missing) {
  print STDERR "Variables needed:\n";
  print STDERR "  $_: $var_help{$_}\n" foreach (sort $needed->elements);
  exit $missing;
}

# Write release notes if non-existent
if (!-e $vars{notes}) {
  die "VISUAL is not set; please set it!\n"
    if -z $ENV{VISUAL};
  system($ENV{VISUAL} . " " . $vars{notes}) == 0 or die "editor failed\n";
}

# Filter out unknown methods
@methods = grep {
  if (!defined ($releaser{$_})) {
    warn "no method $_; ignoring\n";
    0;
  } else {
    1;
  }
} @methods;

# Welease!
for my $method (@methods) {
  $releaser{$method}{getinfo}($releaser{$method})
    if $releaser{$method}{getinfo};
}
for my $method (@methods) {
  $releaser{$method}{release}($releaser{$method});
}
